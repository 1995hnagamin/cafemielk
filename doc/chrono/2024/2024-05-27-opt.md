2024-05-27 `cross3d` の高速化
========

## 環境

```
Gauche scheme shell, version 0.9.15-p1 [utf-8,pthreads], x86_64-apple-darwin21.6.0
(version "0.9.15-p1")
(command "gosh")
(scheme.id gauche)
(languages scheme r5rs r7rs)
(encodings utf-8)
(website "https://practical-scheme.net/gauche")
(build.platform "x86_64-apple-darwin21.6.0")
(build.configure "--prefix=/----" "LDFLAGS=-L/usr/local/opt/bison/lib")
(scheme.path "/----/share/gauche-0.98/site/lib" "/----/share/gauche-0.98/0.9.15-p1/lib")
(threads pthreads)
(gauche.net.tls mbedtls)
```


## 高速化前

現状の `cross3d` の定義は以下の通り。
`main` ブランチでは `define-inline` によるインライン化を行っていないが、比較のためにここではインライン化しておく。

```scheme
;;
;; cafemielk.util
;;

(define-inline (vec3d-tabulate func)
  (vector-map (lambda (indices) (apply func indices))
              #((0 1 2) (1 2 0) (2 0 1))))

(define-inline (cross3d u v)
  (define (u_ i) (vector-ref u i))
  (define (v_ i) (vector-ref v i))
  (vec3d-tabulate
   (lambda (i j k)
     (- (* (u_ j) (v_ k)) (* (u_ k) (v_ j))))))
```

逆アセンブル結果は以下の通り。

```
gosh$ (disasm (^[] (cross3d #(3 141 5) #(92 65 35))))
CLOSURE #<closure (#f)>
=== main_code (name=#f, cc=0x102f19de0, codevec=0x101564240, size=8, const=2 stack=9):
signatureInfo: ((#f))
     0 LREF0-PUSH               ; #:G151
     1 LOCAL-ENV(1)             ; (vec3d-tabulate (lambda (i j k) (- (* (u
     2 LREF11-PUSH              ; #:G152
     3 CONST-PUSH #((0 1 2) (1 2 0) (2 0 1))
     5 GREF-TAIL-CALL(2) #<identifier cafemielk.util#vector-map.13536c0>; (vector-map (lambda (indices) (apply fun
     7 RET
LIFTED CLOSURE #<closure ((#f cross3d) i j k)>
=== main_code (name=(#f cross3d), cc=0x102f19ea0, codevec=0x10311da80, size=23, const=2 stack=3):
signatureInfo: (((#f cross3d) i j k))
     0 CONST-PUSH #(3 141 5)
     2 LREF1                    ; j
     3 VEC-REF                  ; (vector-ref u i)
     4 PUSH
     5 CONST-PUSH #(92 65 35)
     7 LREF0                    ; k
     8 VEC-REF                  ; (vector-ref v i)
     9 NUMMUL2                  ; (* (u_ j) (v_ k))
    10 PUSH
    11 CONST-PUSH #(3 141 5)
    13 LREF0                    ; k
    14 VEC-REF                  ; (vector-ref u i)
    15 PUSH
    16 CONST-PUSH #(92 65 35)
    18 LREF1                    ; j
    19 VEC-REF                  ; (vector-ref v i)
    20 NUMMUL2                  ; (* (u_ k) (v_ j))
    21 NUMSUB2                  ; (- (* (u_ j) (v_ k)) (* (u_ k) (v_ j)))
    22 RET
LIFTED CLOSURE #<closure ((#f #:G152) indices)>
=== main_code (name=(#f #:G152), cc=0x102f19f60, codevec=0x102fabf60, size=4, const=0 stack=2):
signatureInfo: (((#f #:G152) indices))
     0 LREF10-PUSH              ; #:G151
     1 LREF0                    ; indices
     2 TAIL-APPLY(2)            ; (apply func indices)
     3 RET
```

5行目に `GREF-TAIL-CALL(2)` とあり、多分 `vector-map` が呼ばれている。
`(vector-map proc vec)` という呼び出しにおいて `proc` と `vec` が両方コンパイル時に既知なので、直接ベクトルの各成分を計算するように書き換えてほしい。
本当は定数（`#(4610 355 -12777)`）が出力されてほしい。

ベンチマークの結果は以下の通り。

```
;; (use gauche.time)
gosh$ (time-this 1000000 (^[] (cross3d #(3 141 5) #(92 65 35))))
#<time-result 1000000 times/  1.094 real/  1.220 user/  0.010 sys>
```

剛性行列を計算するときの外積の計算回数は結構多い（はず）ので気になる。

## 高速化後

`vec3d-tabulate` は3引数関数を受け取ってベクトルの各成分を計算する。
これを使うのをやめて、マクロで直接ベクトルを生成するように書き換える。

```scheme
;; マクロ定義を `cross3d` の前に書かないと動かない
(define-syntax vec3d-tab
  (syntax-rules ()
    ((_ (i j k) expr)
     (let-syntax
         ((component (syntax-rules ()
                       ((_ i j k) expr))))
       (vector (component 0 1 2)
               (component 1 2 0)
               (component 2 0 1))))))

(define-inline (cross3d u v)
  (define (u_ i) (vector-ref u i))
  (define (v_ i) (vector-ref v i))
  (vec3d-tab
   (i i+1 i+2)
   (- (* (u_ i+1) (v_ i+2)) (* (u_ i+2) (v_ i+1)))))
```

マクロ `vec3d-tab` の中でローカルのマクロ `component` を作っている。
外積以外の処理にも使えるようにこのような一般的な形で定義しておく。例：

```
(vec3d-tab (i i+1 i+2) (- (vector-ref u i+2) (vector-ref v i)))
;; => (vector (- (vector-ref u 2) (vector-ref v 0))
;;            (- (vector-ref u 0) (vector-ref v 1))
;;            (- (vector-ref u 1) (vector-ref v 2)))
```

逆アセンブル結果は以下の通り。
引数が定数であることを見抜いて最初から `#(4610 355 -12777)` を返すようになった。

```
gosh$ (disasm (^[] (cross3d #(3 141 5) #(92 65 35))))
CLOSURE #<closure (#f)>
=== main_code (name=#f, cc=0x102e9dc60, codevec=0x1032f3870, size=5, const=0 stack=2):
signatureInfo: ((#f))
     0 CONSTI-PUSH(4610)
     1 CONSTI-PUSH(355)
     2 CONSTI(-12777)
     3 VEC(3)                   ; (vector (component 0 1 2) (component 1 2
     4 RET
```

ベンチマークの結果は以下の通り。
約12.6倍速くなった。

```
gosh$ (time-this 1000000 (^[] (cross3d #(3 141 5) #(92 65 35))))
#<time-result 1000000 times/  0.087 real/  0.150 user/  0.000 sys>
```


## 付録： `trig2d-area`

### 高速化前

```scheme
(define-inline (cross2d u v)
  (- (* (vector-ref u 0) (vector-ref v 1))
     (* (vector-ref u 1) (vector-ref v 0))))

(define-inline (trig2d-area trig)
  (define (x_ i) (vector-ref trig i))
  (define (y_ i) (vector-ref trig (+ i 3)))
  (define (dx_ i j) (- (x_ j) (x_ i)))
  (define (dy_ i j) (- (y_ j) (y_ i)))
  (* 1/2 (cross2d (vector (dx_ 0 1) (dy_ 0 1))
                  (vector (dx_ 0 2) (dy_ 0 2)))))
```

逆アセンブル結果は以下の通り。

```
gosh$ (disasm (^[] (trig2d-area  #(3 141 592 653 589 793))))
CLOSURE #<closure (#f)>
=== main_code (name=#f, cc=0x103000360, codevec=0x103150e00, size=54, const=2 stack=20):
signatureInfo: ((#f))
     0 LOCAL-ENV-CLOSURES(2) ()
     2 LREF11                   ; #:G231
     3 ENV-SET(1)
     4 LREF10                   ; #:G230
     5 ENV-SET(0)
     6 CONST-PUSH 1/2
     8 PRE-CALL(2) 52           ; (cross2d (vector (dx_ 0 1) (dy_ 0 1)) (v
    10 PRE-CALL(2) 16           ; (dx_ 0 1)
    12 CONSTI-PUSH(0)
    13 CONSTI-PUSH(1)
    14 LREF11                   ; #:G231
    15 LOCAL-ENV-CALL(2)        ; (dx_ 0 1)
    16 PUSH-PRE-CALL(2) 22      ; (dy_ 0 1)
    18 CONSTI-PUSH(0)
    19 CONSTI-PUSH(1)
    20 LREF10                   ; #:G230
    21 LOCAL-ENV-CALL(2)        ; (dy_ 0 1)
    22 VEC(2)                   ; (vector (dx_ 0 1) (dy_ 0 1))
    23 PUSH-PRE-CALL(2) 29      ; (dx_ 0 2)
    25 CONSTI-PUSH(0)
    26 CONSTI-PUSH(2)
    27 LREF11                   ; #:G231
    28 LOCAL-ENV-CALL(2)        ; (dx_ 0 2)
    29 PUSH-PRE-CALL(2) 35      ; (dy_ 0 2)
    31 CONSTI-PUSH(0)
    32 CONSTI-PUSH(2)
    33 LREF10                   ; #:G230
    34 LOCAL-ENV-CALL(2)        ; (dy_ 0 2)
    35 VEC(2)                   ; (vector (dx_ 0 2) (dy_ 0 2))
    36 PUSH-LOCAL-ENV(2)        ; (cross2d (vector (dx_ 0 1) (dy_ 0 1)) (v
    37 LREF1                    ; u
    38 VEC-REFI(0)              ; (vector-ref u 0)
    39 PUSH
    40 LREF0                    ; v
    41 VEC-REFI(1)              ; (vector-ref v 1)
    42 NUMMUL2                  ; (* (vector-ref u 0) (vector-ref v 1))
    43 PUSH
    44 LREF1                    ; u
    45 VEC-REFI(1)              ; (vector-ref u 1)
    46 PUSH
    47 LREF0                    ; v
    48 VEC-REFI(0)              ; (vector-ref v 0)
    49 NUMMUL2                  ; (* (vector-ref u 1) (vector-ref v 0))
    50 NUMSUB2                  ; (- (* (vector-ref u 0) (vector-ref v 1))
    51 RET
    52 NUMMUL2                  ; (* 1/2 (cross2d (vector (dx_ 0 1) (dy_ 0
    53 RET
LIFTED CLOSURE #<closure ((#f dy_) i j)>
=== main_code (name=(#f dy_), cc=0x103000420, codevec=0x102f57f00, size=11, const=1 stack=2):
signatureInfo: (((#f dy_) i j))
     0 CONST-PUSH #(3 141 592 653 589 793)
     2 LREF0-NUMADDI(3)         ; (+ i 3)
     3 VEC-REF                  ; (vector-ref trig (+ i 3))
     4 PUSH
     5 CONST-PUSH #(3 141 592 653 589 793)
     7 LREF1-NUMADDI(3)         ; (+ i 3)
     8 VEC-REF                  ; (vector-ref trig (+ i 3))
     9 NUMSUB2                  ; (- (y_ j) (y_ i))
    10 RET
LIFTED CLOSURE #<closure ((#f dx_) i j)>
=== main_code (name=(#f dx_), cc=0x1030004e0, codevec=0x102f57300, size=11, const=1 stack=2):
signatureInfo: (((#f dx_) i j))
     0 CONST-PUSH #(3 141 592 653 589 793)
     2 LREF0                    ; j
     3 VEC-REF                  ; (vector-ref trig i)
     4 PUSH
     5 CONST-PUSH #(3 141 592 653 589 793)
     7 LREF1                    ; i
     8 VEC-REF                  ; (vector-ref trig i)
     9 NUMSUB2                  ; (- (x_ j) (x_ i))
    10 RET
```

ベンチマークの結果は以下の通り。

```
gosh$ (time-this 1000000 (^[] (trig2d-area  #(3 141 592 653 589 793))))
#<time-result 1000000 times/  0.622 real/  0.820 user/  0.020 sys>
```

### 高速化後

```scheme
(define-inline (cross2d u v)
  (- (* (vector-ref u 0) (vector-ref v 1))
     (* (vector-ref u 1) (vector-ref v 0))))

(define-inline (trig2d-xref trig i)
  (vector-ref trig i))

(define-inline (trig2d-yref trig i)
  (vector-ref trig (+ i 3)))

(define-inline (trig2d-area trig)
  (define-syntax dd
    (syntax-rules ()
      ((_ i j)
       (vector (- (trig2d-xref trig j) (trig2d-xref trig i))
               (- (trig2d-yref trig j) (trig2d-yref trig i))))))
  (* 1/2 (cross2d (dd 0 1) (dd 0 2))))
```

逆アセンブル結果は以下の通り。
4〜6行目、8〜10行目などで一部定数畳み込みが行われているように見えるがなんかやたら長い。

```
gosh$ (disasm (^[] (trig2d-area  #(3 141 592 653 589 793))))
CLOSURE #<closure (#f)>
=== main_code (name=#f, cc=0x10302af60, codevec=0x101630b40, size=29, const=1 stack=15):
signatureInfo: ((#f))
     0 CONST-PUSH 1/2
     2 PRE-CALL(2) 27           ; (cross2d (dd 0 1) (dd 0 2))
     4 CONSTI-PUSH(138)
     5 CONSTI(-64)
     6 VEC(2)                   ; (vector (- (trig2d-xref trig 1) (trig2d-
     7 PUSH
     8 CONSTI-PUSH(589)
     9 CONSTI(140)
    10 VEC(2)                   ; (vector (- (trig2d-xref trig 2) (trig2d-
    11 PUSH-LOCAL-ENV(2)        ; (cross2d (dd 0 1) (dd 0 2))
    12 LREF1                    ; u
    13 VEC-REFI(0)              ; (vector-ref u 0)
    14 PUSH
    15 LREF0                    ; v
    16 VEC-REFI(1)              ; (vector-ref v 1)
    17 NUMMUL2                  ; (* (vector-ref u 0) (vector-ref v 1))
    18 PUSH
    19 LREF1                    ; u
    20 VEC-REFI(1)              ; (vector-ref u 1)
    21 PUSH
    22 LREF0                    ; v
    23 VEC-REFI(0)              ; (vector-ref v 0)
    24 NUMMUL2                  ; (* (vector-ref u 1) (vector-ref v 0))
    25 NUMSUB2                  ; (- (* (vector-ref u 0) (vector-ref v 1))
    26 RET
    27 NUMMUL2                  ; (* 1/2 (cross2d (dd 0 1) (dd 0 2)))
    28 RET
```

ベンチマークの結果は以下の通り。
あんまり速くなってない（2.5倍）。

```
gosh$ (time-this 1000000 (^[] (trig2d-area  #(3 141 592 653 589 793))))
#<time-result 1000000 times/  0.250 real/  0.340 user/  0.010 sys>
```

## リンク集

Gauche 関係。

- Gauche Reference
  + [4.10 定義](https://practical-scheme.net/gauche/man/gauche-refj/Ding-Yi-.html)
  + [5.2 衛生的マクロ](https://practical-scheme.net/gauche/man/gauche-refj/Wei-Sheng-De-makuro.html)
  + [9.36 gauche.time - 時間の計測](https://practical-scheme.net/gauche/man/gauche-refj/Shi-Jian-noJi-Ce-.html)
- WiLiKi (practical-scheme.net)
  + [Gauche:VM命令セットの変更とビルド](https://practical-scheme.net/wiliki/wiliki.cgi?Gauche%3AVM%E5%91%BD%E4%BB%A4%E3%82%BB%E3%83%83%E3%83%88%E3%81%AE%E5%A4%89%E6%9B%B4%E3%81%A8%E3%83%93%E3%83%AB%E3%83%89)
  + [Gauche:Inlining](https://practical-scheme.net/wiliki/wiliki.cgi?Gauche%3AInlining)
  + [Gauche:Translation:Devlog:手続きインライナの改善](https://practical-scheme.net/wiliki/wiliki.cgi?Gauche%3ATranslation%3ADevlog%3A%E6%89%8B%E7%B6%9A%E3%81%8D%E3%82%A4%E3%83%B3%E3%83%A9%E3%82%A4%E3%83%8A%E3%81%AE%E6%94%B9%E5%96%84)

Common Lisp 関係。
`vector-map` の呼び出しが除去されなかったとき Common Lisp に移行しようと思ったが、SBCL/CLISP でも別に最適化されなかった。
高速化のためにマクロを書くなら Scheme でもいいと思ったため移行しなかった。

- [いまから始めるCommon Lisp](https://qiita.com/t-sin/items/054c2ff315ec3b9d3bdc)
- [2024年のCommon Lispの処理系はこれだ!](https://zenn.dev/g000001/articles/e4eb6ab1c9040f03eafa)
- [M.Hiroi's Home Page: Common Lisp Programming](http://www.nct9.ne.jp/m_hiroi/clisp/index.html)
  + [レキシカルスコープとクロージャ](http://www.nct9.ne.jp/m_hiroi/clisp/abcl13.html)
  + [構造体](http://www.nct9.ne.jp/m_hiroi/clisp/abcl17.html)
  + [パッケージの基本的な使い方](http://www.nct9.ne.jp/m_hiroi/clisp/abcl26.html)
- [2.9.1 Advanced iteration methods](https://www.lispworks.com/documentation/lcl50/loop/loop-50.html)
- [Common Lispで高速行列演算](https://keens.github.io/blog/2016/02/04/common_lispdekousokugyouretsuenzan/)
- [Common Lispのdeclareについて](http://mojavy.com/blog/2012/11/09/common-lisp-declare/)
- [型宣言を付けたら遅くなった話](https://gist.github.com/y2q-actionman/7bf396edf8a09264c32c)
- [マクロを定義するマクロ](http://www.asahi-net.or.jp/~kc7k-nd/onlispjhtml/macroDefiningMacros.html)
- [Quicklispで自作モジュールを使用する](https://qiita.com/tamurashingo@github/items/0284c086c51e12e29240)

--------
Author: hnagamin.
Permanent ID of this document: `6042855eb32fde0cad288e6f5ceeb87bdc47dd60`.
Date: 2024-05-27.
